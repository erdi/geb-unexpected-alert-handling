import org.apache.tools.ant.taskdefs.condition.Os
import org.apache.commons.io.FileUtils

apply plugin: "idea"
apply plugin: "groovy"
apply plugin: "io.ratpack.ratpack-base"

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://oss.jfrog.org/oss-snapshot-local/"
        }
    }
    dependencies {
        classpath "io.ratpack:ratpack-gradle:0.9.10-SNAPSHOT"
        classpath "commons-io:commons-io:2.4"
    }
}

task downloadChromeDriver {
    def outputFile = file("$buildDir/webdriver/chromedriver.zip")
    def chromeDriverVersion = "2.10"
    inputs.property("chromeDriverVersion", chromeDriverVersion)
    outputs.file(outputFile)

    doLast {
        def driverOsFilenamePart
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            driverOsFilenamePart = "win32"
        } else if (Os.isFamily(Os.FAMILY_MAC)) {
            driverOsFilenamePart = "mac32"
        } else if (Os.isFamily(Os.FAMILY_UNIX)) {
            driverOsFilenamePart = Os.isArch("amd64") ? "linux64" : "linux32"
        }
        FileUtils.copyURLToFile(new URL("http://chromedriver.storage.googleapis.com/${chromeDriverVersion}/chromedriver_${driverOsFilenamePart}.zip"), outputFile)
    }
}

task unzipChromeDriver(type: Copy) {
    def outputDir = file("$buildDir/webdriver/chromedriver")
    dependsOn downloadChromeDriver
    outputs.dir(outputDir)

    from(zipTree(downloadChromeDriver.outputs.files.singleFile))
    into(outputDir)
}

task configureTest {
    dependsOn unzipChromeDriver
    doLast {
        test.jvmArgs "-Dwebdriver.chrome.driver=${unzipChromeDriver.outputs.files.asFileTree.singleFile}"
    }
}

test {
    dependsOn configureTest
}

repositories {
    maven {
        url "https://oss.jfrog.org/oss-snapshot-local/"
    }
    mavenCentral()
}

dependencies {
    testCompile "org.gebish:geb-junit4:0.9.3",
            "junit:junit:4.11",
            ratpack.dependency("groovy-test"),
            "org.seleniumhq.selenium:selenium-chrome-driver:2.43.1"

}
